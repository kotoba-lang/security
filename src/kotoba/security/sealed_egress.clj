(ns kotoba.security.sealed-egress
  "One authenticated block envelope for every private-data egress seam."
  (:require [clojure.edn :as edn]
            [kotoba.security.cold-tier-admission :as admission])
  (:import [java.nio ByteBuffer]
           [java.security SecureRandom]
           [javax.crypto Cipher]
           [javax.crypto.spec GCMParameterSpec SecretKeySpec]))

(def policy-path "qualification/sealed-egress-policy.edn")
(def magic (.getBytes "KSB1" "UTF-8"))

(defn read-policy []
  (edn/read-string (slurp policy-path)))

(defn- cipher
  [mode key nonce aad]
  (doto (Cipher/getInstance "AES/GCM/NoPadding")
    (.init mode (SecretKeySpec. key "AES") (GCMParameterSpec. 128 nonce))
    (.updateAAD aad)))

(defn seal
  [key key-version plaintext]
  (when-not (= 32 (alength ^bytes key))
    (throw (ex-info "AES-256 block key required" {:code :seal/invalid-key})))
  (when-not (and (integer? key-version) (pos? key-version))
    (throw (ex-info "Positive key version required"
                    {:code :seal/invalid-key-version})))
  (let [nonce (byte-array 12)
        _ (.nextBytes (SecureRandom.) nonce)
        header (doto (ByteBuffer/allocate 8)
                 (.put magic)
                 (.putInt key-version))
        header-bytes (.array header)
        ciphertext (.doFinal (cipher Cipher/ENCRYPT_MODE key nonce header-bytes)
                             ^bytes plaintext)
        output (ByteBuffer/allocate
                (+ (alength header-bytes) (alength nonce) (alength ciphertext)))]
    (-> output (.put header-bytes) (.put nonce) (.put ciphertext) .array)))

(defn open
  [key envelope]
  (let [buffer (ByteBuffer/wrap ^bytes envelope)
        observed-magic (byte-array 4)
        _ (.get buffer observed-magic)
        key-version (.getInt buffer)
        nonce (byte-array 12)
        _ (.get buffer nonce)
        ciphertext (byte-array (.remaining buffer))
        _ (.get buffer ciphertext)
        header (doto (ByteBuffer/allocate 8)
                 (.put observed-magic)
                 (.putInt key-version))
        header-bytes (.array header)]
    (when-not (java.util.Arrays/equals magic observed-magic)
      (throw (ex-info "Unknown sealed envelope"
                      {:code :seal/invalid-magic})))
    {:key-version key-version
     :plaintext (.doFinal (cipher Cipher/DECRYPT_MODE key nonce header-bytes)
                          ciphertext)}))

(defn emit!
  "Seal PRIVATE bytes before invoking the seam adapter SINK."
  [policy cold-policy {:keys [seam key-descriptor key key-version plaintext sink]}]
  (when-not (contains? (:private-egress-seams policy) seam)
    (throw (ex-info "Unknown private egress seam"
                    {:code :egress/unknown-seam :seam seam})))
  (when-not (admission/valid-key? cold-policy key-descriptor)
    (throw (ex-info "Active block-key descriptor required"
                    {:code :egress/block-key-required :seam seam})))
  (when-not (and (bytes? key) (= 32 (alength ^bytes key)))
    (throw (ex-info "Resolved AES-256 key required"
                    {:code :egress/invalid-resolved-key :seam seam})))
  (when-not (bytes? plaintext)
    (throw (ex-info "Private egress payload must be bytes"
                    {:code :egress/invalid-payload :seam seam})))
  (let [envelope (seal key key-version plaintext)]
    (sink {:seam seam
           :sealed? true
           :key-id (:key-id key-descriptor)
           :key-version key-version
           :bytes envelope})))
