(ns kotoba.security.hybrid-vectors
  "Structural checks for hybrid KEM test-vector files (kotoba-lang/kotoba#264).

  Validates conformance/crypto/vectors/*.edn shape without any crypto
  dependency, so the default test suite and scripts/check-crypto-inventory.bb
  can gate the vectors while staying Bouncy-Castle-free. Cryptographic
  recomputation lives in test-vectors/ under the :vectors alias
  (docs/hybrid-envelope-vectors.md)."
  (:require [kotoba.security.crypto-policy :as policy]))

(def hybrid-kem [:x25519 :ml-kem-768])

(def input-hex-lengths
  "Required :vector/inputs fields -> exact hex-string length.
  X25519 keys are 32 bytes; ML-KEM-768 encapsulation key 1184 bytes,
  (expanded) decapsulation key 2400 bytes, ciphertext 1088 bytes."
  {:x25519/recipient-public 64
   :x25519/recipient-secret 64
   :x25519/ephemeral-public 64
   :x25519/ephemeral-secret 64
   :ml-kem/encapsulation-key 2368
   :ml-kem/decapsulation-key 4800
   :ml-kem/ciphertext 2176})

(def expected-hex-lengths
  "Required :vector/expected fields -> exact hex-string length (32 bytes each)."
  {:x25519/shared-secret 64
   :ml-kem/shared-secret 64
   :kek 64})

(defn hex-string?
  [s length]
  (and (string? s)
       (= length (count s))
       (every? #(or (<= (int \0) (int %) (int \9))
                    (<= (int \a) (int %) (int \f)))
               s)))

(defn- hex-field-error
  [m lengths where]
  (some (fn [[k length]]
          (when-not (hex-string? (get m k) length)
            (policy/invalid "hybrid vector hex field invalid"
                            {:where where :field k :expected-length length
                             :value (get m k)})))
        lengths))

(defn vector-error
  [v]
  (or
   (when-not (and (string? (:vector/id v)) (seq (:vector/id v)))
     (policy/invalid "hybrid vector id required" {:vector v}))
   (when-not (nat-int? (:vector/epoch v))
     (policy/invalid "hybrid vector epoch required" {:id (:vector/id v)}))
   (when-not (= hybrid-kem (:vector/kem v))
     (policy/invalid "hybrid vector kem list must be [:x25519 :ml-kem-768]"
                     {:id (:vector/id v) :value (:vector/kem v)}))
   (when-not (hex-string? (:vector/seed v) 64)
     (policy/invalid "hybrid vector seed required" {:id (:vector/id v)}))
   (hex-field-error (:vector/inputs v) input-hex-lengths (:vector/id v))
   (hex-field-error (:vector/expected v) expected-hex-lengths (:vector/id v))
   (let [envelope (get-in v [:vector/expected :envelope])
         hybrid-policy {:kotoba.security/crypto-policy-version 1
                        :mode :hybrid-required
                        :hybrid-epoch-floor 1}
         result (policy/check-envelope hybrid-policy envelope)]
     (or
      (when-not (:valid? result)
        (policy/invalid "hybrid vector envelope rejected under :hybrid-required"
                        {:id (:vector/id v) :result result}))
      (when-not (= (:vector/epoch v) (:envelope/epoch envelope))
        (policy/invalid "hybrid vector envelope epoch mismatch"
                        {:id (:vector/id v) :envelope-epoch (:envelope/epoch envelope)}))
      (when-not (and (true? (:envelope/kem? envelope))
                     (true? (:envelope/hybrid? envelope)))
        (policy/invalid "hybrid vector envelope must be hybrid kem"
                        {:id (:vector/id v) :envelope envelope}))))))

(defn vector-file-error
  [vectors]
  (or
   (when-not (and (vector? vectors) (>= (count vectors) 3))
     (policy/invalid "at least 3 hybrid vectors required"
                     {:count (count vectors)}))
   (when-not (apply distinct? (map :vector/id vectors))
     (policy/invalid "hybrid vector ids must be distinct"
                     {:ids (mapv :vector/id vectors)}))
   (some vector-error vectors)))

(defn check-vector-file
  "Validates the parsed hybrid vector file (a vector of vector maps).
  Returns {:valid? true} or {:valid? false :message .. :data ..}."
  [vectors]
  (if-let [error (vector-file-error vectors)]
    error
    {:valid? true}))
