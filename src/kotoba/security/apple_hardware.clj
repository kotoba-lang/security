(ns kotoba.security.apple-hardware
  "Adapter from the Apple Secure Enclave probe to common HSM evidence.

  Apple EC signing can satisfy hardware-backed/non-export/signing controls,
  but it deliberately cannot claim ML-KEM, general HSM attestation, rotation,
  or outage qualification."
  (:require [clojure.edn :as edn]
            [clojure.java.shell :as shell]))

(defn probe
  ([] (probe "scripts/qualify-apple-secure-enclave.swift"))
  ([script]
   (let [{:keys [exit out err]} (shell/sh "/usr/bin/swift" script)]
     (if (seq out)
       (assoc (edn/read-string out)
              :apple/process-exit exit
              :apple/process-error (when (seq err) err))
       {:apple/qualified? false :apple/process-exit exit
        :apple/process-error err}))))

(defn hardware-evidence
  [result]
  {:provider-id (:apple/provider-id result)
   :hardware-backed? (:apple/hardware-backed? result)
   ;; The failed external representation is the local non-export probe, not
   ;; a vendor attestation certificate.
   :attestation-verified? false
   :private-exported? (not (:apple/non-exportable? result))
   :sign-verified? (:apple/sign-verified? result)
   :kem-verified? false
   :rotation-drill-passed? false
   :outage-failed-closed? false})
