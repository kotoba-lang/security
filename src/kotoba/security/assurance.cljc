(ns kotoba.security.assurance
  (:require [clojure.set :as set]))

(def required-profile-fields
  #{:assurance/score :assurance/maturity :assurance/grade
    :assurance/release-gate :assurance/evidence
    :assurance/residual-critical-gaps})

(def grade-order [:S :A :B :C :D :F])
(def evidence-order [:E0 :E1 :E2 :E3 :E4 :E5])

(defn grade-for-score [model score]
  (:id (first (filter #(>= score (:minimum %)) (:grades model)))))

(defn cap-grade [grade maximum-grade]
  (let [rank (zipmap grade-order (range))]
    (if (< (rank grade) (rank maximum-grade)) maximum-grade grade)))

(defn expected-grade [model control-scores score]
  (let [base (grade-for-score model score)
        critical-values (map control-scores (:critical-controls model))]
    (if (some #(< % 40) critical-values)
      (cap-grade base :C)
      base)))

(defn expected-gate [model control-scores profile]
  (let [critical-values (map control-scores (:critical-controls model))
        evidence-rank (zipmap evidence-order (range))]
    (cond
      (some #(< % 20) critical-values) :fail
      (or (< (evidence-rank (:assurance/evidence profile)) (evidence-rank :E4))
          (pos? (:assurance/residual-critical-gaps profile)))
      :partial
      :else :pass)))

(defn weights-valid? [model controls]
  (let [weights (get-in model [:score :weights])]
    (and (= (set controls) (set (keys weights)))
         (= 1 (reduce + (vals weights)))
         (every? pos? (vals weights)))))

(defn excluded-controls [model]
  (if (= :exclude-and-renormalize (get-in model [:score :na-policy]))
    (get-in model [:applicability :accepted-na-controls] #{})
    #{}))

(defn weighted-score [model scores]
  (let [excluded (excluded-controls model)
        applicable (apply dissoc (get-in model [:score :weights]) excluded)
        denominator (reduce + (vals applicable))]
    (when-not (pos? denominator)
      (throw (ex-info "assurance model has no applicable controls"
                      {:excluded-controls excluded})))
    (/ (reduce-kv (fn [total control weight]
                    (+ total (* weight (get scores control 0))))
                  0 applicable)
       denominator)))

(defn rounded-score [model scores]
  (#?(:clj Math/round :cljs js/Math.round)
   (double (weighted-score model scores))))

(defn heat [model score]
  (some (fn [color]
          (let [minimum (get-in model [:heatmap color :minimum])]
            (when (and minimum (>= score minimum)) color)))
        [:green :yellow :orange :red]))

(defn model-problems [model controls]
  (let [version (:model/version model)
        excluded (excluded-controls model)
        weights (set (keys (get-in model [:score :weights])))]
    (cond-> []
    (not= :kotoba.security/assurance-model (:model/type model))
    (conj :model-type)
    (not (contains? #{1 2} version)) (conj :model-version)
    (not (weights-valid? model controls)) (conj :weights)
    (and (= 2 version)
         (not= :exclude-and-renormalize (get-in model [:score :na-policy])))
    (conj :na-policy)
    (and (= 2 version)
         (or (empty? excluded) (not (set/subset? excluded weights))))
    (conj :accepted-na-controls)
    (not= required-profile-fields (set (:required-report-fields model)))
    (conj :report-fields))))

(defn profile-problems [model profile]
  (let [missing (set/difference required-profile-fields (set (keys profile)))
        maturity (set (map :id (:maturity model)))
        evidence (set (map :id (:evidence model)))
        grades (set (map :id (:grades model)))
        gates (set (:release-gates model))]
    (cond-> []
      (seq missing) (conj [:missing missing])
      (not (<= 0 (:assurance/score profile -1) 100)) (conj :score)
      (not (contains? maturity (:assurance/maturity profile))) (conj :maturity)
      (not (contains? evidence (:assurance/evidence profile))) (conj :evidence)
      (not (contains? grades (:assurance/grade profile))) (conj :grade)
      (not (contains? gates (:assurance/release-gate profile))) (conj :gate)
      (not (integer? (:assurance/residual-critical-gaps profile)))
      (conj :residual-critical-gaps))))

(defn semantic-profile-problems [model control-scores profile]
  (cond-> []
    (not= (:assurance/score profile) (rounded-score model control-scores))
    (conj :weighted-score)
    (not= (:assurance/grade profile)
          (expected-grade model control-scores (:assurance/score profile)))
    (conj :critical-grade-cap)
    (not= (:assurance/release-gate profile)
          (expected-gate model control-scores profile))
    (conj :critical-release-gate)))
