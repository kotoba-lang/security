(ns kotoba.security.bounded-cbor
  "DAG-CBOR admission scanner that rejects attacker-declared allocations
   before handing bytes to the upstream decoder."
  (:require [cbor.core :as cbor])
  (:import [java.nio ByteBuffer]))

(def default-limits
  {:max-input-bytes (* 1024 1024)
   :max-byte-string-bytes (* 1024 1024)
   :max-text-bytes (* 1024 1024)
   :max-collection-items 100000
   :max-depth 128})

(defn- deny! [code data]
  (throw (ex-info "DAG-CBOR admission rejected" (assoc data :code code))))

(defn- require-bytes! [^ByteBuffer buffer n]
  (when (or (neg? n) (> n (.remaining buffer)))
    (deny! :cbor/truncated {:declared n :remaining (.remaining buffer)})))

(defn- read-unsigned! [^ByteBuffer buffer n]
  (require-bytes! buffer n)
  (loop [i 0 value 0N]
    (if (= i n)
      value
      (recur (inc i)
             (+ (* value 256) (bit-and 0xff (int (.get buffer))))))))

(defn- argument! [^ByteBuffer buffer additional]
  (cond
    (< additional 24) additional
    (= additional 24) (read-unsigned! buffer 1)
    (= additional 25) (read-unsigned! buffer 2)
    (= additional 26) (read-unsigned! buffer 4)
    (= additional 27) (read-unsigned! buffer 8)
    (= additional 31) (deny! :cbor/indefinite-length {})
    :else (deny! :cbor/reserved-additional-info {:additional additional})))

(declare scan-item!)

(defn- bounded-int! [value maximum code]
  (when (or (> value maximum) (> value Integer/MAX_VALUE))
    (deny! code {:declared value :maximum maximum}))
  (int value))

(defn- scan-many! [buffer limits depth count]
  (dotimes [_ count]
    (scan-item! buffer limits depth)))

(defn- scan-item! [^ByteBuffer buffer limits depth]
  (when (> depth (:max-depth limits))
    (deny! :cbor/depth {:depth depth :maximum (:max-depth limits)}))
  (require-bytes! buffer 1)
  (let [initial (bit-and 0xff (int (.get buffer)))
        major (unsigned-bit-shift-right initial 5)
        additional (bit-and initial 0x1f)
        value (argument! buffer additional)]
    (case major
      (0 1) nil
      2 (let [n (bounded-int! value (:max-byte-string-bytes limits)
                              :cbor/byte-string-too-large)]
          (require-bytes! buffer n)
          (.position buffer (+ (.position buffer) n)))
      3 (let [n (bounded-int! value (:max-text-bytes limits)
                              :cbor/text-too-large)]
          (require-bytes! buffer n)
          (.position buffer (+ (.position buffer) n)))
      4 (let [n (bounded-int! value (:max-collection-items limits)
                              :cbor/array-too-large)]
          (scan-many! buffer limits (inc depth) n))
      5 (let [n (bounded-int! value (:max-collection-items limits)
                              :cbor/map-too-large)]
          (scan-many! buffer limits (inc depth) (* 2 n)))
      6 (scan-item! buffer limits (inc depth))
      7 (case additional
          24 nil
          25 nil
          26 nil
          27 nil
          nil)
      (deny! :cbor/unknown-major {:major major}))))

(defn decode
  ([bytes] (decode bytes default-limits))
  ([bytes limits]
   (when-not (bytes? bytes)
     (deny! :cbor/input-type {:type (type bytes)}))
   (let [limits (merge default-limits limits)
         size (alength ^bytes bytes)]
     (when (> size (:max-input-bytes limits))
       (deny! :cbor/input-too-large
              {:size size :maximum (:max-input-bytes limits)}))
     (let [buffer (ByteBuffer/wrap ^bytes bytes)]
       (scan-item! buffer limits 0)
       (when (.hasRemaining buffer)
         (deny! :cbor/trailing-bytes {:remaining (.remaining buffer)}))
       (cbor/decode bytes)))))
