(ns kotoba.security.assurance-v2-test
  (:require [clojure.edn :as edn]
            [clojure.test :refer [deftest is testing]]
            [kotoba.security.assurance :as assurance]
            [kotoba.security.score :as score]))

(def v1 (edn/read-string (slurp "policy/security-assurance-model.edn")))
(def v2 (edn/read-string (slurp "policy/security-assurance-model-v2.edn")))
(def score-register (score/read-register))
(def dual (edn/read-string
           (slurp "registers/stack-security-dual-baseline.edn")))

(deftest v2-is-valid-and-renormalizes-only-declared-na
  (let [controls (:assessment/controls score-register)
        all-100 (zipmap controls (repeat 100))]
    (is (= [] (assurance/model-problems v2 controls)))
    (is (= #{:quantum-communication} (assurance/excluded-controls v2)))
    (is (= 100 (assurance/weighted-score v2
                                         (assoc all-100 :quantum-communication 0))))
    (testing "an ordinary missing control still contributes zero"
      (is (< (assurance/weighted-score v2 (dissoc all-100 :hsm)) 100)))
    (testing "the v1 formula remains unchanged"
      (is (= 94 (assurance/rounded-score
                 v1 (assoc all-100 :quantum-communication 0)))))))

(deftest accepted-na-is-backed-by-the-authoritative-gap-register
  (let [accepted (->> (:assessment/gaps score-register)
                      (filter #(= :accepted-non-goal (:gap/status %)))
                      (map :gap/control)
                      set)]
    (is (= accepted (assurance/excluded-controls v2)))
    (is (= #{:G-009} (get-in dual [:applicability :authority-gap-ids])))))

(deftest dual-baseline-is-reproducible
  (let [profiles (:register/profiles
                  (edn/read-string
                   (slurp "registers/stack-security-assurance.edn")))
        expected-v2 {:kotoba 69 :kotoba-lang 64 :kototama 64 :kotobase 64
                     :aiueos 67 :compiler 73 :kagi 83 :kagitaba 64}]
    (is (= (set (keys (:assessment/repos score-register)))
           (set (keys (:repos dual)))))
    (doseq [[repo scores] (:assessment/repos score-register)]
      (let [row (get-in dual [:repos repo])
            profile (get profiles repo)]
        (is (= (:assurance/score profile) (:v1/score row)) (str repo " v1"))
        (is (= (expected-v2 repo) (assurance/rounded-score v2 scores)
               (:v2/score row)) (str repo " v2"))
        (is (= (- (:v2/score row) (:v1/score row)) (:delta row)))
        (is (= (assurance/grade-for-score v2 (:v2/score row))
               (:v2/numeric-band row)))
        (is (= (:assurance/grade profile) (:operational/grade row)))))))

(deftest numeric-a-does-not-auto-promote-operational-grade
  (let [kagi (get-in dual [:repos :kagi])]
    (is (= :A (:v2/numeric-band kagi)))
    (is (= :B (:operational/grade kagi)))
    (is (= :not-promoted (:promotion/status kagi)))
    (is (= #{:maturity-below-L4 :evidence-below-E4}
           (:promotion/blockers kagi)))))
