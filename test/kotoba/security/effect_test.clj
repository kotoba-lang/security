(ns kotoba.security.effect-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.security.effect :as effect]))

(def request
  {:evaluate (fn [value] {:allowed? (:trusted? value)})
   :request {:trusted? true}
   :approved? :allowed?
   :action :artifact/publish
   :resource "sha256:abc"
   :digest "abc"})

(deftest guarded-effect-runs-only-after-allow
  (is (= {:allowed? true}
         (effect/guard! (assoc request :effect identity))))
  (is (thrown-with-msg? clojure.lang.ExceptionInfo #"denied"
                        (effect/guard! (assoc request
                                             :request {:trusted? false}
                                             :effect (fn [_] :bypass))))))

(deftest grants-are-one-shot-and-claim-bound
  (let [grant (effect/issue! request)]
    (is (thrown-with-msg? clojure.lang.ExceptionInfo #"denied"
                          (effect/consume! grant
                                           {:action :artifact/delete
                                            :resource "sha256:abc"
                                            :digest "abc"
                                            :effect (fn [_] :bad)})))
    (testing "claim mismatch burns the grant"
      (is (thrown-with-msg? clojure.lang.ExceptionInfo #"denied"
                            (effect/consume! grant
                                             {:action :artifact/publish
                                              :resource "sha256:abc"
                                              :digest "abc"
                                              :effect (fn [_] :replay)}))))))
