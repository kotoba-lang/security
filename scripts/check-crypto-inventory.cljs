#!/usr/bin/env nbb
;; Crypto inventory gate (kotoba-lang/kotoba#264).
;;
;; Usage (run from the repo root):
;;   nbb --classpath src scripts/check-crypto-inventory.cljs
;;       [crypto-policy.edn crypto-inventory.edn]
;;
;; Validates registers/crypto-inventory.edn against policy/crypto-policy.edn:
;; structure is always enforced; FIPS strictness (no :crypto/fips-status
;; :not-claimed entries) only when the policy mode is :fips-required.
;; Also structurally validates conformance/crypto/vectors/*.edn hybrid KEM
;; vector files (fields, kem list, hex shapes) — BC-free; cryptographic
;; recomputation runs under `clojure -M:vectors:vectors-test`.
;; Exit 1 on failure.
(ns kotoba.security.scripts.check-crypto-inventory
  (:require ["node:fs" :as fs]
            ["node:path" :as path]
            [clojure.edn :as edn]
            [clojure.string :as str]
            [kotoba.security.crypto-policy :as policy]
            [kotoba.security.hybrid-vectors :as hv]))

(def script-file (or *file* (first (.-argv js/process))))
(def script-dir (path/dirname (path/resolve script-file)))
(def root (path/resolve script-dir ".."))

(def cli-args
  (try
    (vec *command-line-args*)
    (catch :default _
      (vec (drop 2 (js->clj (.-argv js/process)))))))

(def policy-file
  (if (first cli-args)
    (path/resolve (first cli-args))
    (path/resolve root "policy/crypto-policy.edn")))

(def inventory-file
  (if (second cli-args)
    (path/resolve (second cli-args))
    (path/resolve root "registers/crypto-inventory.edn")))

(defn- unblob [v]
  (if (string? v)
    (try (let [parsed (edn/read-string v)] (if (coll? parsed) parsed v))
         (catch :default _ v))
    v))

(defn- tx-data? [content]
  (and (vector? content) (seq content) (map? (first content))
       (contains? (first content) :db/id)))

(defn- reconstitute-entity [ns-name tx-data]
  (into {}
        (map (fn [[k v]]
               [(if (= ns-name (namespace k)) (keyword (name k)) k)
                (unblob v)]))
        (dissoc (first tx-data) :db/id)))

(defn read-edn
  ([f] (read-edn f nil))
  ([f ns-name]
   (let [content (edn/read-string (fs/readFileSync f "utf8"))]
     (if (and ns-name (tx-data? content))
       (reconstitute-entity ns-name content)
       content))))

(def crypto-policy (read-edn policy-file "policy.crypto-policy"))
(def inventory (read-edn inventory-file "registers.crypto-inventory"))
(def result (policy/check-inventory crypto-policy inventory))

(if (:valid? result)
  (do (doseq [entry (:inventory inventory)]
        (println "ok" (:crypto/use entry)
                 (str "pq=" (:crypto/pq-status entry))
                 (str "fips=" (:crypto/fips-status entry))))
      (println "ok crypto-inventory" (str "mode=" (:mode crypto-policy))))
  (do (println "FAIL" (:message result) (pr-str (:data result)))
      (.exit js/process 1)))

;; Hybrid KEM vector files: structural gate.
(let [vectors-dir (path/resolve root "conformance/crypto/vectors")
      vector-files (->> (js->clj (fs/readdirSync vectors-dir))
                        (filter #(str/ends-with? % ".edn"))
                        (map #(path/join vectors-dir %))
                        (sort))]
  (when (empty? vector-files)
    (println "FAIL hybrid vector files required in conformance/crypto/vectors/")
    (.exit js/process 1))
  (doseq [f vector-files]
    (let [vectors (read-edn f)
          check (hv/check-vector-file vectors)
          name (path/basename f)]
      (if (:valid? check)
        (println "ok" name
                 (str "vectors=" (count vectors))
                 (str "kem=" (pr-str hv/hybrid-kem)))
        (do (println "FAIL" name
                     (:message check) (pr-str (:data check)))
            (.exit js/process 1))))))
